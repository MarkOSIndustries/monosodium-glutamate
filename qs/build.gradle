plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.9.23'
  id 'distribution'
}

repositories {
  mavenCentral()
  maven {
    // Needed while clikt hasn't released https://github.com/ajalt/clikt/pull/508
    url = uri("https://oss.sonatype.org/content/repositories/snapshots/")
  }
}

dependencies {
  implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib'
  testImplementation group: 'junit', name: 'junit', version: '4.11'
  testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit'

  implementation group: 'org.rocksdb', name: 'rocksdbjni', version: '6.11.4'
  implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.5.1'
  implementation project(':clikt')
  implementation project(':encodings-clikt')
  implementation project(':grpc')
  implementation project(':schemas')
  implementation group: 'io.grpc', name: 'grpc-netty', version: '1.14.0'
}

build {
  dependsOn("installDist")
}

jar {
  manifest {
    attributes(
      'Class-Path': configurations.runtimeClasspath.collect { 'dependencies/' + it.getName() }.join(' '),
      'Implementation-Title': project.name,
      'Implementation-Version': project.version,
      'Main-Class': 'msg.qs.MainKt',
    )
  }
}

distributions {
  main {
    distributionBaseName = project.name
    contents {
      into("") {
        from jar.archiveFile
      }
      into("dependencies") {
        from configurations.runtimeClasspath
      }
    }
  }
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}
