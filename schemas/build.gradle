plugins {
  id 'com.google.protobuf' version '0.9.4'
  id 'java'
  id 'java-library'
  id 'idea'
}

repositories {
  mavenCentral()
}

dependencies {
  api group: 'com.google.protobuf', name: 'protobuf-java', version: protobufVersion
  api group: 'io.grpc', name: 'grpc-protobuf', version: grpcVersion
  api group: 'io.grpc', name: 'grpc-stub', version: grpcVersion

  // Java 9+ requires this to be here for GRPC to compile properly
  // https://github.com/grpc/grpc-java/issues/3633
  implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
}

sourceSets {
  main {
    proto {
      include 'google/**/*.proto'
    }
  }
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:$protobufVersion"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {
      }
    }
  }
  generatedFilesBaseDirProperty = "$projectDir/gen"
}

clean {
  delete protobuf.generatedFilesBaseDir
}

idea {
  module {
    // proto files and generated Java files are automatically added as
    // source dirs.
    // If you have additional sources, add them here:
//    sourceDirs += file("$projectDir/something/else/");
  }
}
