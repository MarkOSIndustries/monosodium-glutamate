plugins {
  id 'com.google.protobuf' version '0.8.16'
  id 'java'
  id 'idea'
}

repositories {
  mavenCentral()
}

dependencies {
  compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.17.3'
  compile group: 'io.grpc', name: 'grpc-protobuf', version: '1.38.1'
  compile group: 'io.grpc', name: 'grpc-stub', version: '1.38.1'

  // Java 9+ requires this to be here for GRPC to compile properly
  // https://github.com/grpc/grpc-java/issues/3633
  compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
}

sourceSets {
  main {
    proto {
      include 'google/**/*.proto'
    }
  }
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:3.17.0"
  }
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.38.0'
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {
      }
    }
  }
  generatedFilesBaseDir = "$projectDir/gen"
}

clean {
  delete protobuf.generatedFilesBaseDir
}

idea {
  module {
    // proto files and generated Java files are automatically added as
    // source dirs.
    // If you have additional sources, add them here:
//    sourceDirs += file("$projectDir/somethign/else/");
  }
}
