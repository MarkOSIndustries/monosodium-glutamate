plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.9.23'
  id 'distribution'
}

repositories {
  mavenCentral()
  maven {
    // Needed while clikt hasn't released https://github.com/ajalt/clikt/pull/508
    url = uri("https://oss.sonatype.org/content/repositories/snapshots/")
  }
}

dependencies {
  implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib'
  testImplementation group: 'junit', name: 'junit', version: '4.11'
  testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit'

  implementation group: 'org.apache.kafka', name: 'kafka-clients', version: kafkaVersion
  implementation group: 'org.slf4j', name: 'slf4j-nop', version: '1.7.25'
  implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.5.1'
  implementation project(':clikt')
  implementation project(':grpc')
  implementation project(':kafka')
  implementation project(':kafka-clikt')
  implementation project(':schemas')
  implementation group: 'io.grpc', name: 'grpc-netty', version: grpcVersion
}

build {
  dependsOn("installDist")
}

jar {
  manifest {
    attributes(
      'Class-Path': configurations.runtimeClasspath.collect { 'dependencies/' + it.getName() }.join(' '),
      'Implementation-Title': project.name,
      'Implementation-Version': project.version,
      'Main-Class': 'msg.kgb.MainKt',
    )
  }
}

distributions {
  main {
    distributionBaseName = project.name
    contents {
      into("") {
        from jar.archiveFile
      }
      into("dependencies") {
        from configurations.runtimeClasspath
      }
    }
  }
}

compileKotlin {
    kotlinOptions.jvmTarget = targetJvmVersion
}
compileTestKotlin {
    kotlinOptions.jvmTarget = targetJvmVersion
}
